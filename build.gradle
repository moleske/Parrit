import java.time.LocalDateTime
import java.time.ZoneId
import java.time.ZoneOffset

group "com.example"
version "0.0.1-SNAPSHOT"
description "parrit"

buildscript {
    ext {
        springBootVersion = "1.3.6.RELEASE"
        kotlin_version = '1.0.3'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "java"
apply plugin: 'kotlin'
apply plugin: "spring-boot"


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-security:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    compile "org.postgresql:postgresql:9.4.1208.jre7"

    testCompile(group: "org.springframework.boot", name: "spring-boot-starter-test", version:springBootVersion) {
        exclude(module: "commons-logging")
    }

    compile "org.apache.commons:commons-lang3:3.4"
    compile "org.flywaydb:flyway-core:3.2.1"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "io.damo.kspec:kspec-spring:1.2.1"
    testCompile "org.jetbrains.kotlin:kotlin-test:${kotlin_version}"
}

test {
    filter {
        includeTestsMatching "*Test"
    }
}

task jsTest(type: Exec) {
    workingDir project.projectDir
    commandLine "./node_modules/karma/bin/karma", "start", "karma.conf.js"
}

test.dependsOn(jsTest)

task gruntBuild(type: Exec) {
    workingDir project.projectDir
    commandLine "./node_modules/grunt-cli/bin/grunt", "build"
}

task jsBuild(type: Copy, dependsOn: gruntBuild) {
    from "$project.projectDir/src/main/resources/static/built"
    into "$project.projectDir/build/classes/main/static/built"
}

jar.dependsOn(jsBuild)

task createMigration << {
    def utc = ZoneId.of("UTC")
    def timestamp = LocalDateTime.now(utc).toEpochSecond(ZoneOffset.UTC)
    def path = "src/main/resources/db/migration/V${timestamp}__${migration}.sql"

    "touch $project.projectDir/$path".execute()
    println path
}

task deploy(type: Exec, dependsOn: build) {
    workingDir project.projectDir
    commandLine "cf", "push"
}

defaultTasks "clean", "build"
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}
